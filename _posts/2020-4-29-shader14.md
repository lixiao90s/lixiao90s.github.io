---
title: shader入门基础-透明效果三
author: 晓
date: 2020-04-29 14:36:00 +0800
categories: [unity]
tags: [shader]
---

本文内容主要参考 《Unity shader 入门精要》一书，旨在总结所学知识与加深个人理解

## 透明度混合

这种方法可以得到真正的半透明效果，它会使用当前片元的透明度作为混合因子，与已经存储在颜色缓冲中的颜色进行混合，得到新的颜色。
但是透明度混合需要关闭深度写入，这使得我们要非常小心物体的渲染顺序。
混合就和两个操作数有关 **源颜色(source color)**, **目标颜色(destination color)**。源颜色 用 **S** 表示，指由片元着色器产生的颜色值；
目标颜色 **D**,指的从颜色缓冲中读取到的颜色值。对他们进行混合后得到的输出颜色，用 **O**表示，它会重新写入到颜色缓冲中。
当我们使用 Blend 命令时，混合会自动开启。

## shaderLab 中设置混合因子的命令

| 命令                                            | 描述                                                                                                                                        |
| ----------------------------------------------- | ------------------------------------------------------------------------------------------------------------------------------------------- |
| Blend SrcFactor DstFactor                       | $$\mathtt O_{rgb}=SrcFatcor*\mathtt S_{rgb}+DstFactor*\mathtt D_{rgb})$$                                                                    |
| Blend SrcFactor DstFactor,SrcFactorA DstFactorA | $$\mathtt O_{rgb}=SrcFatcor*\mathtt S_{rgb}+DstFactor*\mathtt D_{rgb})\quad \mathtt O_a = SrcFactorA* \mathtt S_a+DstFactorA*\mathtt D_a $$ |

![alphatest]({{ "/assets/img/sample/blendfactor.png" | relative_url }})  
![alphatest]({{ "/assets/img/sample/blendop.png" | relative_url }})  
![alphatest]({{ "/assets/img/sample/blendeffect.png" | relative_url }})

## 透明度混合实践

```c
Shader "Practice/AlphaBlend"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _Color("Color Tint",Color) = (1,1,1,1)
        _AlphaScale("Alpha Cutoff",Range(0,1))=0.5
    }
    SubShader
    {
        //RenderType 可以让unity把这个shader提前归入到“Transparent”组中
        //"IgnoreProjector"="True" 不受投影器影响
        //透明度混合使用的是 渲染序列是Transparent
        Tags { "Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent" }
        Pass
        {
            //LightModel 是 pass标签的一种，正确定义LightModel 才能正确得到unity的内置光照变量
            Tags {"LightModel"="ForwardBase"}
            //关闭深度写入
            ZWrite Off
            //开启混合，等价于下列公式
            // O_rgb= srcAlpha*S_rgb+(1-srcAlpha)*D_rgb
            //O_a=srcAlpha*S_a+(1-srcAlpha)*D_a
            //这里可以合并rgb 和a，标量和矢量相乘
            //O_rgba = srcAlpha*S_rgba+(1-srcAlpha)*D_rgba
            Blend srcAlpha OneMinusSrcAlpha

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"
            //包含内置变量的 _LightColor0
            #include "Lighting.cginc"
            //C_diffuse = (C_light·m_diffuse)max(0,N·L) //漫反射计算公式
            //都在unity 世界坐标计算，需要知道顶点在世界的位置，顶点法线，顶点视角位置
            struct appdata
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord :TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD1;
                float4 pos : SV_POSITION;
                float3 worldNormal:NORMAL;
                float3 worldPos:TEXCOORD2;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed4 _Color;
            fixed _AlphaScale;

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                //把顶点坐标转换到世界坐标，为后续取 L做准备
                o.worldPos = mul(unity_ObjectToWorld,v.vertex);
                //把法线转换到 世界空间中
                o.worldNormal = UnityObjectToWorldNormal(v.normal);
                //纹理映射坐标，该顶点在纹理中对应的2D坐标
                o.uv=v.texcoord.xy*_MainTex_ST.xy+_MainTex_ST.zw;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                //逐像素 光照
                //C_diffuse = (C_light·m_diffuse)max(0,N·L)
                //获取公式中的各个变量，m_diffuse =此时材质面板给定的_Color*顶点的color
                fixed4 col;
                fixed4 texColor = tex2D(_MainTex,i.uv);
                fixed3 N = normalize(i.worldNormal);
                //UnityWorldSpaceLightDir输入世界空间中的坐标点，WorldSpaceLightDir 输入模型空间中的坐标顶点
                fixed3 L = normalize(UnityWorldSpaceLightDir(i.worldPos));
                //clip(texColor.a-_AlphaScale);
                //equal to
                //if(m_diffuse.a-_AlphaScale)<0{
                    //discard;
                //}
                //计算漫反射部分

                fixed3 c_light = _LightColor0.rgb;
                fixed3 m_diffuse =texColor.rgb*_Color.rgb;
                fixed3 c_diffuse = c_light*m_diffuse*max(0,dot(N,L));
                //计算环境光产生的影响,环境光反射的也会受材质本身颜色影响
                fixed3 ambient = m_diffuse*UNITY_LIGHTMODEL_AMBIENT.rgb;
                //环境光和漫反射光
                //只有使用Blend命令打开混合后，我们在这里设置透明通道才有意义，否则
                //这些透明度不会对片元透明效果有任何影响。
                col = fixed4(ambient+c_diffuse,texColor.a*_AlphaScale);
                return col;
            }
            ENDCG
        }
    }
}

```

效果如图
![alphaBlend]({{ "/assets/img/sample/alphaBlend.gif" | relative_url }})

```c
//Blend srcAlpha OneMinusSrcAlpha
```

屏蔽 Blend 命令，可以发现半透明效果消失了。只有使用 Blend 命令打开混合后，这里透明度才会对片元透明效果有任何影响。
![noblend]({{ "/assets/img/sample/noblend.png" | relative_url }})
